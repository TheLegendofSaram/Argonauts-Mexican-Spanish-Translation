plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.2-SNAPSHOT" apply false

    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0" // literally never gonna update
}

architectury {
    minecraft = rootProject.minecraft_version
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven { url = "https://maven.resourcefulbees.com/repository/maven-public/" }
        maven {
            url = 'https://jitpack.io'
            content {
                includeGroup 'com.github.LlamaLad7'
                includeGroup 'com.github.llamalad7.mixinextras'
            }
        }
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = "UTF-8"
            options.release = 17
        }
    }

    java {
        withSourcesJar()
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "maven-publish"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
        }
        modApi "com.teamresourceful.resourcefullib:resourcefullib-$name-$rootProject.minecraft_version:$rootProject.resourcefullib_version"
        modApi("earth.terrarium.heracles:heracles-$name-$rootProject.minecraft_version:$rootProject.heracles_version") {
            exclude group: "com.teamresourceful.resourcefullib", module: "resourcefullib-$name-$rootProject.minecraft_version"
            exclude group: "earth.terrarium.cadmus", module: "cadmus-$name-$rootProject.minecraft_version"
        }
        implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-$name:$rootProject.mixinextras_version"))
    }

    var env = System.getenv()
    var modLoader = name

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact remapJar
                artifact(sourcesJar) {
                    builtBy remapSourcesJar
                }

                artifactId = "$mod_id-$modLoader-$minecraft_version"

                pom {
                    name = github_name
                    description = ''
                    url = "https://github.com/terrarium-earth/$github_name"

                    scm {
                        connection = "git:https://github.com/terrarium-earth/${github_name}.git"
                        developerConnection = "git:https://github.com/terrarium-earth/${github_name}.git"
                        url = "https://github.com/terrarium-earth/$github_name"
                    }

                    licenses {
                        license {
                            name = 'MIT'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = "https://maven.resourcefulbees.com/repository/terrarium/"
                credentials {
                    username env.MAVEN_USER
                    password env.MAVEN_PASS
                }
            }
        }
    }
}